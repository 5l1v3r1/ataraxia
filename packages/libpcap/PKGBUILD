# Maintainer: nee-san <nagakamira@gmail.com>

pkgname=libpcap
pkgver=1.9.0
pkgrel=1
pkgdesc="A system-independent interface for user-level packet capture"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://www.tcpdump.org/"
license=('BSD')
depends=('musl' 'libusb' 'libnl')
makedepends=('flex')
source=("https://www.tcpdump.org/release/$pkgname-$pkgver.tar.gz")
md5sums=('dffd65cb14406ab9841f421732eb0f33')

build() {
	cd "$srcdir"/$pkgname-$pkgver
	CFLAGS="$CFLAGS -I${srcdir}/include $(pkg-config libnl-genl-3.0 --cflags)" \
	./configure \
		--prefix='' \
		--with-libnl \
		--with-pcap=linux \
		--enable-ipv6 \
		--disable-dbus \
		--disable-bluetooth \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	mkdir -p "$pkgdir"/include/net
	cd "$pkgdir"/include/net
	ln -s ../pcap-bpf.h bpf.h
}
